plugins {
	// add 'apply false' for building as a library spring boot project
	id 'org.springframework.boot' version '2.7.1' apply false
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	// add this for using JitPack
	id 'maven-publish'
}

group = 'com.swetroye'
version = '0.0.9-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

// add this for building as a library spring boot project
dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive:2.7.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// add this for using JitPack
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

tasks.named('test') {
	useJUnitPlatform()
}


jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Will include every single one of your dependencies, project or not
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it)}        
    }
}